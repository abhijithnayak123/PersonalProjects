IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[USP_Lookup_Program]') AND type in (N'P'))
DROP PROCEDURE [dbo].[USP_Lookup_Program]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/*****************************************************************************************************************                                               
* Procedure Name  : USP_Lookup_Program
* File Name		  : USP_Lookup_Program.SQL      
* Description     : This SP loads the newly added Programs into the Program Lookup Table
* Created By      : Veera
* Created Date    : May/8/2014
*-----------------------------------------------------------------------------------------------------------------          
** Date:        Author:			Bug #		Description:										Reviewed By
** ----------   -------------	------		---------------------------------------------------	-----------                                           
*  
*******************************************************************************************************************/

CREATE PROCEDURE USP_Lookup_Program(@RunID INT)
AS
BEGIN
	SET NOCOUNT ON
	
	DECLARE @TableName VARCHAR(100) ='TBL_Lookup_Program'
	DECLARE @RowCount  INT = 0
	DECLARE @LogMessage varchar(100)
	
	BEGIN TRY
		
		SET @LogMessage = 'Incremental data load - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Started',0,0,  @LogMessage, @RunID
	
		--Inserts the Program columns 
		--  which are not existing in Programs lookup table
		INSERT INTO TBL_Lookup_Program(AllianceNumber,ContactID)
		SELECT 	DISTINCT
					 P.AllianceNumber
					,P.ContactID	
							
		FROM		dbo.SYN_IT_AllianceNumbers P
		
		INNER JOIN	dbo.SYN_IT_ContactMaster	CM 
		ON			P.CONTACTID = CM.CONTACTID
		
		LEFT JOIN	dbo.TBL_Lookup_Program LP
		ON			P.AllianceNumber = LP.AllianceNumber
		
		WHERE		LP.AllianceNumber IS NULL
	
		
		SET @RowCount=@@RowCount
		SET @LogMessage = 'Incremental data loaded - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Completed',0,@RowCount, @LogMessage, @RunID  
		
		--TBL_Lookup_Program_DeletedRecord
		SET @RowCount=0
		SET @TableName ='TBL_Lookup_Program_DeletedRecord'
		
		DELETE		 LP
		OUTPUT		 Deleted.AllianceNumber
					,Deleted.ContactID
					,Deleted.ProgramID
					,GETDATE() 
		INTO TBL_Lookup_Program_DeletedRecord(AllianceNumber ,ContactID ,ProgramID, DeletedDate)
		FROM		dbo.TBL_Lookup_Program	LP
		LEFT JOIN	dbo.SYN_IT_AllianceNumberS		T 
					ON LP.AllianceNumber = T.AllianceNumber
		WHERE		T.AllianceNumber IS NULL
		
		--LOGGING
		SET @RowCount=@@RowCount
		SET @LogMessage = 'Incremental data loaded - Physical Delete - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName, 'Completed', 0, @RowCount, @LogMessage, @RunID 
		
		RETURN 0
		
	END TRY
	BEGIN CATCH
		DECLARE @ErrorMessage	NVARCHAR(max)
		DECLARE @ErrorSeverity	INT
		DECLARE @ErrorState		INT

		SET  @ErrorMessage	= 'Incremental data load - [ ' + @TableName + ' ] - Error: ' + Error_Message()
		SET	 @ErrorSeverity	= ERROR_SEVERITY()
		SET	 @ErrorState	= ERROR_STATE()
	    
		EXEC USP_Lookup_RefreshLog @TableName,'Error',0,0, 'Error' , @RunID         
	    
		RAISERROR (@ErrorMessage,	-- Message text
				   @ErrorSeverity,	-- Severity
				   @ErrorState		-- State
				   );    
	END CATCH
END