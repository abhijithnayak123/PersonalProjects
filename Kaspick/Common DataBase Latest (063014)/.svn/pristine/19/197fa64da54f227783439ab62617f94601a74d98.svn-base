IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[USP_Lookup_Remainderman]') AND type in (N'P'))
DROP PROCEDURE [dbo].[USP_Lookup_Remainderman]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/*****************************************************************************************************************                                               
* Procedure Name  : USP_Lookup_Remainderman
* File Name		  : USP_Lookup_Remainderman.SQL      
* Description     : This SP loads the newly added Remainderman into the Remainderman Lookup Table
* Created By      : Veera
* Created Date    : May/8/2014
*-----------------------------------------------------------------------------------------------------------------          
** Date:        Author:			Bug #		Description:										Reviewed By
** ----------   -------------	------		---------------------------------------------------	-----------                                           
*  
*******************************************************************************************************************/

CREATE PROCEDURE USP_Lookup_Remainderman(@RunID INT)
AS
BEGIN
	SET NOCOUNT ON
	
	DECLARE @TableName VARCHAR(100) ='TBL_Lookup_Remainderman'
	DECLARE @RowCount  INT = 0
	DECLARE @LogMessage varchar(100)
	
	BEGIN TRY
		
		SET @LogMessage = 'Incremental data load - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Started',0,0,  @LogMessage, @RunID

		--Inserts the Remainderman columns and the appropriate lookup Columns
		--  which are not existing in Remainderman lookup table
		INSERT INTO TBL_Lookup_Remainderman(ContactID,CustomerAccountNumber,ContactRoleCode,AccountID,IsExternal)
		SELECT		DISTINCT
					 CAR.ContactID
					,CAR.CustomerAccountNumber
					,CAR.ContactRoleCode
					,ALP.AccountID
					,CASE WHEN AC.ContactID IS NOT NULL THEN 0 ELSE 1 END AS IsExternal
				
		FROM		dbo.SYN_IT_ContactAccountRoles CAR
		
		INNER JOIN	dbo.SYN_IT_ContactRoleCodes	CRC 
					ON	CAR.ContactRoleCode = CRC.ID

		INNER JOIN	dbo.SYN_IT_AccountMaster A 
					ON	A.CustomerAccountNumber=CAR.CustomerAccountNumber
					
		INNER JOIN	dbo.SYN_IT_AccountManagerCodes AMC
					ON	A.ManagerCode=AMC.ManagerCode
					
		INNER JOIN	dbo.TBL_Lookup_Account ALP
					ON	CAR.CustomerAccountNumber = ALP.CustomerAccountNumber	
					
		INNER JOIN	dbo.SYN_IT_ContactMaster CM 
					ON	CAR.ContactID=CM.ContactID
				
		LEFT JOIN	dbo.SYN_IT_AccountManagerCodes AC 
					ON	CM.ContactID=AC.ContactID			
	
		LEFT JOIN	dbo.TBL_Lookup_Remainderman LP
					ON 	CAR.ContactID = LP.ContactID
					AND	CAR.CustomerAccountNumber = LP.CustomerAccountNumber
					AND	CAR.ContactRoleCode	= LP.ContactRoleCode
	
		WHERE		CRC.ContactRoleCodeDesc = 'Remainderman'
		AND			LP.ContactID IS NULL
		
		SET @RowCount=@@RowCount
		SET @LogMessage = 'Incremental data loaded - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Completed',0,@RowCount, @LogMessage, @RunID  
		
		--TBL_Lookup_Remainderman_DeletedRecord
		SET @RowCount=0		
		SET @TableName ='TBL_Lookup_Remainderman_DeletedRecord'

		DELETE		LP
		OUTPUT		 Deleted.ContactID
					,Deleted.CustomerAccountNumber
					,Deleted.ContactRoleCode
					,Deleted.RemaindermanID
					,Deleted.AccountID
					,Deleted.IsExternal
					,GETDATE() 
		INTO TBL_Lookup_Remainderman_DeletedRecord(ContactID ,CustomerAccountNumber ,ContactRoleCode ,RemaindermanID ,AccountID ,IsExternal, DeletedDate)
		FROM		dbo.TBL_Lookup_Remainderman	LP
		LEFT JOIN	dbo.SYN_IT_ContactAccountRoles	T 
					ON 	LP.ContactID = T.ContactID
					AND	LP.CustomerAccountNumber = T.CustomerAccountNumber
					AND	LP.ContactRoleCode	= T.ContactRoleCode		
		WHERE		T.ContactID IS NULL
		
		--LOGGING
		SET @RowCount=@@RowCount

		SET @LogMessage = 'Incremental data loaded - Physical Delete - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName, 'Completed', 0, @RowCount, @LogMessage, @RunID 
		
		RETURN 0
		
	END TRY
	BEGIN CATCH
		DECLARE @ErrorMessage	NVARCHAR(max)
		DECLARE @ErrorSeverity	INT
		DECLARE @ErrorState		INT

		SET  @ErrorMessage	= 'Incremental data load - [ ' + @TableName + ' ] - Error: ' + Error_Message()
		SET	 @ErrorSeverity	= ERROR_SEVERITY()
		SET	 @ErrorState	= ERROR_STATE()
	    
		EXEC USP_Lookup_RefreshLog @TableName,'Error',0,0, 'Error'   ,@RunID        
	    
		RAISERROR (@ErrorMessage,	-- Message text
				   @ErrorSeverity,	-- Severity
				   @ErrorState		-- State
				   );    
	END CATCH
END