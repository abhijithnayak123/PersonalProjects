/****** Object:  StoredProcedure [dbo].[USP_IE_ImportSaveSTGTransaction]    Script Date: 07/02/2014 09:22:21 ******/
IF EXISTS (
		SELECT 1
		FROM sys.objects
		WHERE object_id = OBJECT_ID(N'[dbo].[USP_IE_ImportSaveSTGTransaction]')
			AND type IN (
				N'P'
				,N'PC'
				)
		)
	DROP PROCEDURE [dbo].[USP_IE_ImportSaveSTGTransaction]
GO

/****** Object:  StoredProcedure [dbo].[USP_IE_ImportSaveSTGTransaction]    Script Date: 07/02/2014 09:22:21 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/******************************************************************************    
** Name:     USP_IE_ImportSaveSTGTransaction    
** Short Desc:   Imports Axys Transactions into transactions staging table  TBL_IE_STG_Transaction.
**    
** Full Description : Imports Axys Transactions into transactions staging table TBL_IE_STG_Transaction.
**            
**    
** Sample Call    
  DECLARE @XMLDATA XML
	SET @XMLDATA = 
'<StgTransactionsCollection><InsertList>
	<StgTransactions StagingID="1" TransactionID="0"  GroupID="1"  RunDate="5/31/2007 12:00:00 AM"  CustomerAccountNumber="MLAMAW"  CUSIP="" TranCode="li"  Comment=""  SecType="caus"  SecuritySymbol="CSca"   ImportDate="10/1/2007 5:50:47 PM"  UserID="1"  UserName="Administrator"  />
	<StgTransactions StagingID="2" TransactionID="0"  GroupID="1"  RunDate="5/31/2007 12:00:00 AM"  CustomerAccountNumber="MLAMAW"  CUSIP="" TranCode="by"  Comment=""  SecType="agus"  SecuritySymbol="NAES.X"  ImportDate="10/1/2007 5:50:48 PM"  UserID="1"  UserName="Administrator"  />
	<StgTransactions StagingID="3" TransactionID="0"  GroupID="1"  RunDate="5/31/2007 12:00:00 AM"  CustomerAccountNumber="MLAMAW"  CUSIP="" TranCode="by"  Comment=""  SecType="mjus"  SecuritySymbol="VWEH.X"  ImportDate="10/1/2007 5:50:48 PM"  UserID="1"  UserName="Administrator"  />
	<StgTransactions StagingID="4" TransactionID="0"  GroupID="1"  RunDate="5/31/2007 12:00:00 AM"  CustomerAccountNumber="MLAMAW"  CUSIP="" TranCode="by"  Comment=""  SecType="mgus"  SecuritySymbol="PTTR.X"  ImportDate="10/1/2007 5:50:48 PM"  UserID="1"  UserName="Administrator"  />
</InsertList><UpdateList></UpdateList><DeleteList></DeleteList></StgTransactionsCollection>'
	
    EXEC USP_IE_ImportSaveSTGTransaction @XMLDATA
**    
** Return values: NONE    
**    
**    
** Standard declarations    
**       SET LOCK_TIMEOUT         30000   -- 30 seconds    
**     r
** Created By: Mohamed Salih    
** Company   : Kaspick & Company    
** Project   : Back Office Integration - Income Estimation    
** Created DT: 07/15/2014    
**                
*******************************************************************************    
**       Change History    
*******************************************************************************    
** Date:        Author:  Bug #     Description:                           Rvwd    
** --------     -------- ------    -------------------------------------- --------    
**   
*******************************************************************************    
** Copyright (C) <CopyrightYear,,Year> Kaspick & Company, All Rights Reserved    
** COMPANY CONFIDENTIAL -- NOT FOR DISTRIBUTION    
*******************************************************************************/
CREATE PROCEDURE [dbo].[USP_IE_ImportSaveSTGTransaction] @XMLDATA XML
AS
BEGIN
	SET NOCOUNT ON;
	SET LOCK_TIMEOUT 30000;-- 30 seconds    

	BEGIN TRY
		BEGIN TRANSACTION

		INSERT INTO [TBL_IE_STG_Transaction] (
			[StagingID]
			,[GroupID]
			,[RunDate]
			,[CustomerAccountNumber]
			,[CUSIP] 
			,[TranCode]
			,[SecType]
			,[SecuritySymbol]
			,[ImportDate]
			,[UserID]
			,[UserName]
			)
		SELECT XmlInput.Item.value('@StagingID[1]', 'int') AS StagingID
			,XmlInput.Item.value('@GroupID[1]', 'int') AS GroupID
			,XmlInput.Item.value('@RunDate[1]', 'datetime') AS RunDate
			,XmlInput.Item.value('@CustomerAccountNumber[1]', 'varchar(14)') AS CustomerAccountNumber
			,XmlInput.Item.value('@CUSIP[1]', 'varchar(12)') AS CUSIP
			,XmlInput.Item.value('@TranCode[1]', 'varchar(3)') AS TranCode
			,XmlInput.Item.value('@SecType[1]', 'varchar(5)') AS SecType
			,XmlInput.Item.value('@SecuritySymbol[1]', 'varchar(50)') AS SecuritySymbol
			,XmlInput.Item.value('@ImportDate[1]', 'datetime') AS ImportDate
			,XmlInput.Item.value('@UserID[1]', 'int') AS UserID
			,XmlInput.Item.value('@UserName[1]', 'varchar(50)') AS UserName								
		FROM @XMLDATA.nodes('//StgTransactionsCollection/InsertList/StgTransactions') AS XmlInput(Item)

		COMMIT TRANSACTION;
	END TRY

	BEGIN CATCH
		ROLLBACK TRANSACTION;

		DECLARE @ErrorMessage NVARCHAR(4000);
		DECLARE @ErrorSeverity INT;
		DECLARE @ErrorState INT;

		SELECT @ErrorMessage = ERROR_MESSAGE()
			,@ErrorSeverity = ERROR_SEVERITY()
			,@ErrorState = ERROR_STATE();

		RAISERROR (
				@ErrorMessage
				,-- Message text.
				@ErrorSeverity
				,-- Severity.
				@ErrorState -- State.
				);

		PRINT N'The transaction is in an uncommittable state. ' + 'Rolling back transaction.'
	END CATCH;
	SET NOCOUNT OFF;
END
