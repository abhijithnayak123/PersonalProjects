IF EXISTS (
		SELECT *
		FROM sysobjects
		WHERE type = 'P'
			AND NAME = 'USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc'
		)
BEGIN
	DROP PROCEDURE USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc;

	PRINT 'DROPPED USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc';
END
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/******************************************************************************                              
** Name:     USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc                              
** Short Desc: To retrieve Participant Type for Bene Report.             
**                              
** Full Description: To retrieve  details            
**                                      
** Input Arguments:             
**                 
** Sample Call             
**              
 DECLARE @XMLDeliverableItems XML            
    SET @XMLDeliverableItems ='<DeliverableItemsDataToValidateCollection><InsertList></InsertList><UpdateList></UpdateList><DeleteList></DeleteList>            
<SelectList>            
<DeliverableItemsDataToValidate AccountID="204"  BeneficiaryID="585"  ClientEmployeeID="0"  ClientID="1"              
DeliverableItemID="16444"  DeliverableQueueID="2"  DonorID="0"  TrustparticipantID="1250"  DeliverableFrequency="S"  /><DeliverableItemsDataToValidate AccountID="204"  BeneficiaryID="1676"  ClientEmployeeID="0"  ClientID="1"              
DeliverableItemID="7"  DeliverableQueueID="1"  DonorID="117464"  TrustparticipantID="47"              
DeliverableFrequency="A" GiftKey="48205" /><DeliverableItemsDataToValidate AccountID="640"  BeneficiaryID="1663"  ClientEmployeeID="0"  ClientID="1"              
DeliverableItemID="17206"  DeliverableQueueID="1"  DonorID="561"  TrustparticipantID="9"  DeliverableFrequency="A" GiftKey="48205" />            
</SelectList></DeliverableItemsDataToValidateCollection>'             
 EXEC USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc            
    @XMLDeliverableItems            
             
             
**                     
** Return values: Null            
**                              
**                              
** Standard declarations                              
**       SET LOCK_TIMEOUT         30000   -- 30 seconds                              
**                               
** Created By: Venugopal B            
** Company   : Kaspick & Company                              
** Project   : Excelsior  - BeneReport                              
** Created DT: 11/09/2009                              
**                                          
*******************************************************************************                        
**       Change History                              
*******************************************************************************                        
** Date:        Author:  Bug #     Description:                           Rvwd                        
** --------  -------- ------    -------------------------------------- --------                        
** 11/25/2009 Venugopal         ET#10717 - Updated to handle the scenario where change of participant status            
        in excelsior changes donorid or beneficiary ID            
12/18/2009 Venugopal   ET# 11164 - Updated to consider participant satatus from Giftwrap for PIF Reports         
07/26/2012 Ganapati        - Updated the fields as per the modified view V_EIS_EX_GW_GIFTKEY             
24-Jun-2014		Mukesh		 SP Name Renamed and Formatted         
10-Jul-2014		Geervani	 -Changed View Name V_EIS_EX_GW_GIFTKEY to VW_RP_GiftKey use view from KASPICKDB      
*******************************************************************************                              
** Copyright (C) <CopyrightYear,,Year> Kaspick & Company, All Rights Reserved                              
** COMPANY CONFIDENTIAL -- NOT FOR DISTRIBUTION                              
*******************************************************************************/
CREATE PROCEDURE [dbo].[USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc]
	-- paremeters here                  
	@XMLDeliverableItems XML
AS
BEGIN
	--  Initial Set statements  --                  
	--SET NOCOUNT ON;                  
	--SET LOCK_TIMEOUT                30000;   -- 30 seconds                  
	--SET TRANSACTION ISOLATION LEVEL SNAPSHOT;                  
	--  Variable Declarations  --                  
	DECLARE @procname VARCHAR(60);
	DECLARE @ErrorMessage VARCHAR(1000);
	DECLARE @ErrorNumber INT;
	-- Variables used for error handling - uncomment if needed                  
	DECLARE @val1 VARCHAR(30);
	DECLARE @val2 VARCHAR(30);
	--  Temp tables, Cursors, Table Variables  --                  
	DECLARE @DeliverableItemData TABLE (
		DeliverableItemID INT
		,AccountID INT
		,ParticipantID INT
		,DonorID INT
		,BeneficiaryID INT
		,GiftKey INT --ET# 11164            
		);

	--  Variable Data Assignment  --            
	SET @procname = 'USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc';

	-- Body of procedure  --                  
	BEGIN TRY
		INSERT INTO @DeliverableItemData
		SELECT XMLdoc.DeliverableItemData.value('@DeliverableItemID[1]', 'int') AS DeliverableItemID
			,XMLdoc.DeliverableItemData.value('@AccountID[1]', 'int') AS AccountID
			,XMLdoc.DeliverableItemData.value('@TrustparticipantID[1]', 'int') AS ParticipantID
			,XMLdoc.DeliverableItemData.value('@DonorID[1]', 'int') AS DonorID
			,XMLdoc.DeliverableItemData.value('@BeneficiaryID[1]', 'int') AS BeneficiaryID
			,XMLdoc.DeliverableItemData.value('@GiftKey[1]', 'int') AS GiftKey
		FROM @XMLDeliverableItems.nodes('//DeliverableItemsDataToValidateCollection/SelectList/DeliverableItemsDataToValidate') AS XMLdoc(DeliverableItemData)
		INNER JOIN TBL_EIS_DT_Deliverable_Items DIDS ON XMLdoc.DeliverableItemData.value('@DeliverableItemID[1]', 'int') = DIDS.DeliverableItemID
		WHERE DIDS.ReportType = 'B'

		SELECT DID.DeliverableItemID
			,108 AS RuleID
			,'BeneReport' AS Attribute
			,(
				CASE 
					WHEN DGA.AccountType <> 'PIF'
						THEN ISNULL(VTT.PARTICIPANT_TYPE, 'Beneficiary or Donor changed')
					ELSE (
							CASE 
								WHEN ISNULL(VGK.SHORT, '') = ''
									THEN 'Beneficiary Inactive'
								ELSE 'Beneficiary Active'
								END
							)
					END
				) AS ActualValue
			,--ET# 11164            
			'' AS DisplayMessage
		FROM @DeliverableItemData DID
		INNER JOIN DEFERREDGIFTACCOUNT DGA ON DID.AccountID = DGA.AccountID
		LEFT JOIN V_EIS_EX_TRUSTPARTICIPANT_TYPE VTT ON DID.AccountID = VTT.AccountID --ET#10717            
			AND DID.ParticipantID = VTT.ParticipantID
			AND DID.BeneficiaryID = ISNULL(VTT.BeneficiaryID, 0)
			AND DID.DonorID = ISNULL(VTT.DonorID, 0)
		LEFT JOIN V_EIS_EX_GW_GIFTKEY VGK ON DGA.AdventID = VGK.PIFNICKNAME -- Advent ID -- ET# 11164      --Changed View Name to use view from KASPICKDB      
			AND CONVERT(VARCHAR(20), DID.ParticipantID) = VGK.PERSONACCOUNT1
			AND DID.GiftKey = VGK.GIFTKEY
			AND VGK.SHORT LIKE '%Bene-A'
	END TRY

	BEGIN CATCH
		SET @ErrorMessage = ERROR_MESSAGE();
		SET @ErrorNumber = ERROR_NUMBER();
		SET @val1 = '';
		SET @val2 = '';

		EXEC dbo.spSYS_ErrorHandler @codename = @procname
			,@errmsg = @ErrorMessage
			,@errnbr = @ErrorNumber
			,@val1 = ''
			,@val1str = 'USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc: Cannot Select.'
			,@val2 = ''
			,@val2str = '';
	END CATCH
		-- End of procedure  -- 
END
GO

IF EXISTS (
		SELECT *
		FROM sysobjects
		WHERE type = 'P'
			AND NAME = 'USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc'
		)
BEGIN
	PRINT 'CREATED USP_EIS_RPT_BR_VE_UR_BeneReport_SelProc';
END