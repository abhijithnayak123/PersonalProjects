/****** Object:  StoredProcedure [dbo].[USP_PP_GetValidationEngineWireBeneficiaryFileGeneration]    Script Date: 08/28/2013 11:22:21 ******/
IF EXISTS (
		SELECT *
		FROM sys.objects
		WHERE object_id = OBJECT_ID(N'[dbo].[USP_PP_GetValidationEngineWireBeneficiaryFileGeneration]')
			AND type IN (
				N'P'
				,N'PC'
				)
		)
	DROP PROCEDURE [dbo].[USP_PP_GetValidationEngineWireBeneficiaryFileGeneration]
GO

/****** Object:  StoredProcedure [dbo].[USP_PP_GetValidationEngineWireBeneficiaryFileGeneration]    Script Date: 08/28/2013 11:22:21 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/******************************************************************************                      
** Name		 : USP_PP_GetValidationEngineWireBeneficiaryFileGeneration                      
** Short Desc: To Get File Generation Details
**                      
** Full Description:  To Get File Generation Details for Payment Type 'Beneficiarypayment' and 'PGCalcImport'       
**        
**                              
** Input Arguments: 
 @UserID	INT,
 @Type Bit =1--auto 

**         
** Sample Call     

EXEC USP_PP_GetValidationEngineWireBeneficiaryFileGeneration 1  ,1    

                 	
**                      
** Standard declarations                      
**       SET LOCK_TIMEOUT         30000   -- 30 seconds                      
**                       
** Created By: Debajyoti kalita   
** Company   : Kaspick & Company                      
** Project   : BackOffice Integration                      
** Created DT: 28-Aug-13                 
**                                  
*******************************************************************************                
**       Change History                      
*******************************************************************************                
** Date:      Author:	 Bug #     Description:                           
** --------  --------	 ------    -------------------------------------- 
** 29-Jan-2014	Niveditha			Added PDFFileCreationDate and PDFFileName filter
** 6/3/2014		Anand				Changed PaymentMethod data source in filter condition
** 8/21/2014	Anand		        Changed data source in case of Auto and Manual Validation

*******************************************************************************                      
** Copyright (C) <CopyrightYear,,Year> Kaspick & Company, All Rights Reserved                      
** COMPANY CONFIDENTIAL -- NOT FOR DISTRIBUTION                      
*******************************************************************************/
CREATE PROCEDURE [dbo].[USP_PP_GetValidationEngineWireBeneficiaryFileGeneration] @UserID INT
	,@Type BIT = 1 --auto 
AS
BEGIN TRY
	--  Initial Set statements  --      
	SET NOCOUNT ON;
	SET LOCK_TIMEOUT 30000;-- 30 seconds      

	---Declare Variable
	DECLARE @RuleID INT
	DECLARE @CurrentDate DATETIME

	---Assigning value to Variable
	SELECT @RuleID = RuleID
	FROM TBL_PP_ValidationRule
	WHERE RuleName = 'Wire request generation failed'
		AND RuleCategory = 'Beneficiary payment'

	SELECT @CurrentDate = CONVERT(DATETIME, CONVERT(VARCHAR(25), GETDATE(), 101))

	-- Body of procedure  --            
	---Return Data
	IF @Type = 1
	BEGIN
		SELECT 'Beneficiarypayment' AS PaymentType
			,BenPay.BPScheduleID AS ScheduleID
			,@RuleID AS RuleID
			,'WireRequestFileCreationDate' AS Attribute
			,ISNULL(CONVERT(VARCHAR(20), BenPmnt.WirePDFFileCreationDate), 'none') AS ActualValue
		FROM TBL_PP_BeneficiaryPayoutSchedule BenPay 
		INNER JOIN TBL_PP_BeneficiaryPayment BenPmnt ON BenPay.PaymentID = BenPmnt.PaymentID
		WHERE ISNULL(BenPay.PaymentDate, '') <> ''			
			AND BenPmnt.PaymentMethod = 'WIRE'
			AND (
				BenPmnt.WirePDFFileCreationDate IS NULL
				OR BenPmnt.WirePDFFileName IS NULL
				)
			AND (
				@CurrentDate >= CONVERT(DATETIME, CONVERT(VARCHAR(25), BenPay.EPD, 101))
				AND @CurrentDate <= CONVERT(DATETIME, CONVERT(VARCHAR(25), BenPay.PaymentDate, 101))
				)		
		
		UNION ALL
		
		SELECT 'PGCalcImport' AS PaymentType
			,pgcalc.MergedPGCalcPaymentID AS ScheduleID
			,@RuleID AS RuleID
			,'WireRequestFileCreationDate' AS Attribute
			,ISNULL(CONVERT(VARCHAR(20), BenPmnt.WirePDFFileCreationDate), 'none') AS ActualValue
		FROM dbo.TBL_PP_PGCalcPaymentDataMerged pgcalc 
		INNER JOIN TBL_PP_BeneficiaryPayment BenPmnt ON pgcalc.PaymentID = BenPmnt.PaymentID
		WHERE ISNULL(pgcalc.PaymentDate, '') <> ''			
			AND BenPmnt.PaymentMethod = 'WIRE'
			AND (
				BenPmnt.WirePDFFileCreationDate IS NULL
				OR BenPmnt.WirePDFFileName IS NULL
				)
			AND (
				@CurrentDate >= CONVERT(DATETIME, CONVERT(VARCHAR(25), pgcalc.EPD, 101))
				AND @CurrentDate <= CONVERT(DATETIME, CONVERT(VARCHAR(25), pgcalc.PaymentDate, 101))
				)			
	END
	ELSE
	BEGIN
		SELECT PaymentType
			,StgData.ScheduleID
			,@RuleID AS RuleID
			,'WireRequestFileCreationDate' AS Attribute
			,ISNULL(CONVERT(VARCHAR(20), BenPmnt.WirePDFFileCreationDate), 'none') AS ActualValue
		FROM TBL_PP_STG_InnoTrustValidationEngineInput StgData
		INNER JOIN TBL_PP_BeneficiaryPayoutSchedule BenPay ON StgData.ScheduleID = BenPay.BPScheduleID
		INNER JOIN TBL_PP_BeneficiaryPayment BenPmnt ON BenPay.PaymentID = BenPmnt.PaymentID
		WHERE ISNULL(BenPay.PaymentDate, '') <> ''
			AND StgData.PaymentType = 'Beneficiarypayment'
			AND StgData.PaymentMethod = 'WIRE'
			-- 29-Jan-2014 Niveditha: Added PDFFileCreationDate and PDFFileName filter
			AND (
				BenPmnt.WirePDFFileCreationDate IS NULL
				OR BenPmnt.WirePDFFileName IS NULL
				)
			AND (
				@CurrentDate >= CONVERT(DATETIME, CONVERT(VARCHAR(25), BenPay.EPD, 101))
				AND @CurrentDate <= CONVERT(DATETIME, CONVERT(VARCHAR(25), BenPay.PaymentDate, 101))
				)
			AND UserID = @UserID
		
		UNION ALL
		
		SELECT PaymentType
			,StgData.ScheduleID
			,@RuleID AS RuleID
			,'WireRequestFileCreationDate' AS Attribute
			,ISNULL(CONVERT(VARCHAR(20), BenPmnt.WirePDFFileCreationDate), 'none') AS ActualValue
		FROM TBL_PP_STG_InnoTrustValidationEngineInput StgData
		INNER JOIN dbo.TBL_PP_PGCalcPaymentDataMerged pgcalc ON pgcalc.MergedPGCalcPaymentID = StgData.ScheduleID
		INNER JOIN TBL_PP_BeneficiaryPayment BenPmnt ON pgcalc.PaymentID = BenPmnt.PaymentID
		WHERE ISNULL(pgcalc.PaymentDate, '') <> ''
			AND StgData.PaymentType = 'PGCalcImport'
			AND StgData.PaymentMethod = 'WIRE'
			-- 29-Jan-2014 Niveditha: Added PDFFileCreationDate and PDFFileName filter
			AND (
				BenPmnt.WirePDFFileCreationDate IS NULL
				OR BenPmnt.WirePDFFileName IS NULL
				)
			AND (
				@CurrentDate >= CONVERT(DATETIME, CONVERT(VARCHAR(25), pgcalc.EPD, 101))
				AND @CurrentDate <= CONVERT(DATETIME, CONVERT(VARCHAR(25), pgcalc.PaymentDate, 101))
				)
			AND UserID = @UserID
	END
END TRY

BEGIN CATCH
	SELECT ERROR_MESSAGE() AS ErrorMessage
END CATCH
