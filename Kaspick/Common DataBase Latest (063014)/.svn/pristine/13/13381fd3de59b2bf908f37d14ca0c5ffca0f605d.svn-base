/****** Object:  StoredProcedure [dbo].[USP_SIT_SaveMarketValueSummary]    Script Date: 07/02/2014 09:22:21 ******/
IF EXISTS (
		SELECT 1
		FROM sys.objects
		WHERE object_id = OBJECT_ID(N'[dbo].[USP_SIT_SaveMarketValueSummary]')
			AND type IN (
				N'P'
				,N'PC'
				)
		)
	DROP PROCEDURE [dbo].[USP_SIT_SaveMarketValueSummary]
GO

/****** Object:  StoredProcedure [dbo].[USP_SIT_SaveMarketValueSummary]    Script Date: 07/02/2014 09:22:21 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/******************************************************************************    
** Name:     USP_SIT_SaveMarketValueSummary    
** Short Desc:  Saves the MarketValueSummary details for the given valuation IDs.   
**    
** Full Description : Saves the MarketValueSummary details for the given valuation IDs. 
**            
**    
** Sample Call    
  Declare @XMLdata XML
  Declare @ReturnStatus INT
  Declare @ErrorDesc Varchar(100)
  
  SET @XMLdata = '<IncomeAccrualScheduleCollection>
	<InsertList>
	<IncomeAccrualSchedule ValuationID= "1" CustomerAccountNumber="ACADW" />
	</InsertList>
	</IncomeAccrualScheduleCollection>'
        EXEC USP_SIT_SaveMarketValueSummary  @XMLdata , 1, @ReturnStatus OUTPUT , @ErrorDesc OUTPUT
  SELECT @ReturnStatus,@ErrorDesc
**    
** Return values: NONE    
**    
**    
** Standard declarations    
**       SET LOCK_TIMEOUT         30000   -- 30 seconds    
**     
** Created By: Mohamed Salih    
** Company   : Kaspick & Company    
** Project   : Back Office Integration - Sub Accounting   
** Created DT: 09/03/2014      
**                
*******************************************************************************    
**       Change History    
*******************************************************************************    
** Date:        Author:  Bug #     Description:                           Rvwd    
** --------     -------- ------    -------------------------------------- --------    
**   
*******************************************************************************    
** Copyright (C) <CopyrightYear,,Year> Kaspick & Company, All Rights Reserved    
** COMPANY CONFIDENTIAL -- NOT FOR DISTRIBUTION    
*******************************************************************************/
CREATE PROCEDURE [dbo].[USP_SIT_SaveMarketValueSummary] @XMLdata XML
	,@UserID INT
	,@ReturnStatus INT = - 1
OUTPUT
	,@ErrorDesc VARCHAR(8000)
OUTPUT AS

BEGIN
	SET NOCOUNT ON;
	SET LOCK_TIMEOUT 30000;-- 30 seconds    

	--  Variable Declarations  --   
	--  Temp tables, Cursors, Table Variables  --    
	BEGIN TRY
		BEGIN TRANSACTION

		IF OBJECT_ID('tempdb..[#Tmp_MarketValueSummary]') IS NOT NULL
			DROP TABLE [#Tmp_MarketValueSummary]

		IF OBJECT_ID('tempdb..[#Tmp_CustomerAccountNumber]') IS NOT NULL
			DROP TABLE [#Tmp_CustomerAccountNumber]

		CREATE TABLE #Tmp_CustomerAccountNumber (
			ValuationID INT
			,XmlCustomerAccountNumber VARCHAR(14)
			,StatementFrequencyCode CHAR(8)
			,PreviousPeriod DATETIME
			,CurrentPeriod DATETIME
			)

		CREATE TABLE #Tmp_MarketValueSummary (
			ValuationID INT
			,CustomerAccountNumber VARCHAR(14)
			,TotalIncomeEarneda DECIMAL(28, 4)
			,TotalIncomeEarnedb DECIMAL(28, 4)
			,RealizedGainOrLoss DECIMAL(28, 4)
			)

		DECLARE @Colname VARCHAR(100)
		DECLARE @SelColname VARCHAR(1000)
		DECLARE @Qry VARCHAR(1000)
		DECLARE @TableName VARCHAR(100)

		SET @TableName = 'TBL_SIT_EBRMarketValueSummary'

		INSERT INTO #Tmp_CustomerAccountNumber (
			ValuationID
			,XmlCustomerAccountNumber
			,StatementFrequencyCode
			,PreviousPeriod
			,CurrentPeriod
			)
		SELECT XmlInput.Item.value('@ValuationID[1]', 'INT') AS ValuationID
			,XmlInput.Item.value('@CustomerAccountNumber[1]', 'VARCHAR(14)')
			,XmlInput.Item.value('@StatementFrequencyCode[1]', 'CHAR(8)')
			,XmlInput.Item.value('@PreviousPeriod[1]', 'DATETIME')
			,XmlInput.Item.value('@CurrentPeriod[1]', 'DATETIME')
		FROM @XMLDATA.nodes('//MarketValueSummaryParametersCollection/InsertList/MarketValueSummary') AS XmlInput(Item)

		DELETE TBL_SIT_EBRMarketValueSummary
		WHERE ValuationID IN (
				SELECT ValuationID
				FROM #Tmp_CustomerAccountNumber
				)

		SELECT @Colname = ISNULL(@Colname + ',', '') + QUOTENAME(TransactionCode)
		FROM (
			SELECT DISTINCT TransactionCode
			FROM TBL_SIT_InnotrustFieldMapping
			WHERE KaspickTableName = @TableName
			) TmpTranCode

		SELECT @SelColname = ISNULL(@SelColname + ',', '') + QUOTENAME(TransactionCode) + ' as ' + KaspickColumnName
		FROM (
			SELECT DISTINCT TransactionCode
				,ColumnSequence
				,KaspickColumnName
				,ROW_NUMBER() OVER (
					PARTITION BY TransactionCode ORDER BY TransactionCode
					) AS RowNum
			FROM TBL_SIT_InnotrustFieldMapping
			WHERE KaspickTableName = @TableName
			) TmpTranCode
		WHERE Rownum = 1
		ORDER BY ColumnSequence

		SET @Qry = ' SELECT ValuationID,CustomerAccountNumber,' + @SelColname + ' 
		FROM SYN_IT_Stmt_TransactionSummary TranSmry
		INNER JOIN #Tmp_CustomerAccountNumber TmpInput
			ON TranSmry.CustomerAccountNumber = TmpInput.XmlCustomerAccountNumber
		PIVOT(MAX(TransactionTotal) FOR TransactionCategoryCode IN (
		' + @Colname + '
		)) p'

		INSERT INTO #Tmp_MarketValueSummary (
			ValuationID
			,CustomerAccountNumber
			,TotalIncomeEarneda
			,TotalIncomeEarnedb
			,RealizedGainOrLoss
			)
		EXECUTE (@Qry)

		INSERT INTO TBL_SIT_EBRMarketValueSummary (
			ValuationID
			,BeginningMarketValue
			,IncomeEarned
			,RealizedGainOrLoss
			,BeginningUnrealizedGainOrLoss
			,EndingUnrealizedGainOrLoss
			,EndingMarketValue
			,ModifiedDate
			,ModifiedBy
			,CreatedDate
			,CreatedBy
			)
		SELECT ValuationID
			,SUM(ISNULL(PrvPrd.BaseMV, 0))
			,SUM(ISNULL(TotalIncomeEarneda, 0) + ISNULL(TotalIncomeEarnedb, 0))
			,SUM(ISNULL(RealizedGainOrLoss, 0))
			,SUM(ISNULL(PrvPrd.BaseMV, 0))
			,SUM(ISNULL(CurrPrd.BaseMV, 0))
			,SUM(ISNULL(CurrPrd.BaseMV, 0))
			,GETDATE()
			,@UserID
			,GETDATE()
			,@UserID
		FROM #Tmp_MarketValueSummary TmpSchdl
		INNER JOIN #Tmp_CustomerAccountNumber TmpAccnt
			ON TmpSchdl.ValuationID = TmpAccnt.ValuationID
		INNER JOIN SYN_IT_Stmt_Holdings PrvPrd
			ON PrvPrd.CustomerAccountNumber = TmpSchdl.CustomerAccountNumber
				AND PrvPrd.StatementFrequencyCode = TmpAccnt.StatementFrequencyCode
				AND PrvPrd.StatementDate = TmpAccnt.PreviousPeriod
		INNER JOIN SYN_IT_Stmt_Holdings CurrPrd
			ON CurrPrd.CustomerAccountNumber = TmpSchdl.CustomerAccountNumber
				AND CurrPrd.StatementFrequencyCode = TmpAccnt.StatementFrequencyCode
				AND CurrPrd.StatementDate = TmpAccnt.CurrentPeriod
		GROUP BY ValuationID

		SET @ReturnStatus = 0
		SET @ErrorDesc = '';

		IF OBJECT_ID('tempdb..[#Tmp_MarketValueSummary]') IS NOT NULL
			DROP TABLE [#Tmp_MarketValueSummary]

		IF OBJECT_ID('tempdb..[#Tmp_CustomerAccountNumber]') IS NOT NULL
			DROP TABLE [#Tmp_CustomerAccountNumber]

		COMMIT TRANSACTION;
	END TRY

	BEGIN CATCH
		SET @ReturnStatus = - 1;
		SET @ErrorDesc = ERROR_MESSAGE();

		ROLLBACK TRANSACTION;

		DECLARE @ErrorMessage NVARCHAR(4000);
		DECLARE @ErrorSeverity INT;
		DECLARE @ErrorState INT;

		SELECT @ErrorMessage = ERROR_MESSAGE()
			,@ErrorSeverity = ERROR_SEVERITY()
			,@ErrorState = ERROR_STATE();

		RAISERROR (
				@ErrorMessage
				,-- Message text.
				@ErrorSeverity
				,-- Severity.
				@ErrorState -- State.
				);

		PRINT N'The transaction is in an uncommittable state. ' + 'Rolling back transaction.'
	END CATCH;

	SET NOCOUNT OFF;
END
