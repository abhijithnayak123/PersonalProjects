IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[USP_Lookup_RemaindermanDesignation]') AND type in (N'P'))
DROP PROCEDURE [dbo].[USP_Lookup_RemaindermanDesignation]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/*****************************************************************************************************************                                               
* Procedure Name  : USP_Lookup_RemaindermanDesignation
* File Name		  : USP_Lookup_RemaindermanDesignation.SQL      
* Description     : This SP loads the newly added RemaindermanDesignation into the RemaindermanDesignation Lookup Table
* Created By      : Veera
* Created Date    : May/8/2014
*-----------------------------------------------------------------------------------------------------------------          
** Date:        Author:			Bug #		Description:										Reviewed By
** ----------   -------------	------		---------------------------------------------------	-----------                                           
*  
*******************************************************************************************************************/

CREATE PROCEDURE USP_Lookup_RemaindermanDesignation( @RunID INT)
AS
BEGIN
	SET NOCOUNT ON
	
	DECLARE @TableName VARCHAR(100) ='TBL_Lookup_RemaindermanDesignation'
	DECLARE @RowCount  INT = 0
	DECLARE @LogMessage varchar(100)
	
	BEGIN TRY
		
		SET @LogMessage = 'Incremental data load - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Started',0,0,  @LogMessage, @RunID
		
		DECLARE @RemaindermanRoleCode INT
		SET @RemaindermanRoleCode=(SELECT ID FROM dbo.SYN_IT_CONTACTROLECODES WHERE ContactRoleCodeDesc='Remainderman')

		--Inserts the Remainderman Designation columns and the appropriate lookup Columns
		--  which are not existing in Remainderman Designation lookup table
		INSERT INTO TBL_Lookup_RemaindermanDesignation(ContactID,CustomerAccountNumber,ContactRoleCode,RemaindermanID,AccountID)				
		SELECT		DISTINCT	
					 CAR.ContactID
					,CAR.CustomerAccountNumber
					,CAR.ContactRoleCode
					,LPR.RemaindermanID 					
					,LPR.AccountID
		FROM		dbo.SYN_IT_ContactAccountRoles CAR

		INNER JOIN	dbo.SYN_IT_AccountMaster A 
					ON	A.CustomerAccountNumber=CAR.CustomerAccountNumber

		INNER JOIN	dbo.SYN_IT_ContactRoleCodes	CRC 
					ON	CAR.ContactRoleCode = CRC.ID
					
		INNER JOIN	dbo.TBL_Lookup_Remainderman LPR
					ON	CAR.CustomerAccountNumber=LPR.CustomerAccountNumber
					AND	LPR.ContactRoleCode = @RemaindermanRoleCode
					AND	LPR.IsExternal=0			
					
		LEFT JOIN	dbo.TBL_Lookup_RemaindermanDesignation LP
					ON 	CAR.ContactID = LP.ContactID
					AND	CAR.CustomerAccountNumber = LP.CustomerAccountNumber
					AND	CAR.ContactRoleCode	= LP.ContactRoleCode	
								
		WHERE		CRC.ContactRoleCodeDesc = 'Remainderman Designation' 
		AND			LP.ContactID IS NULL
		
		
		SET @RowCount=@@RowCount
		SET @LogMessage = 'Incremental data loaded - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName,'Completed',0,@RowCount, @LogMessage, @RunID  
		
		--TBL_Lookup_RemaindermanDesignation_DeletedRecord
		SET @RowCount=0		
		SET @TableName ='TBL_Lookup_RemaindermanDesignation_DeletedRecord'
		
		DELETE		 LP
		OUTPUT		 Deleted.ContactID
					,Deleted.CustomerAccountNumber
					,Deleted.ContactRoleCode
					,Deleted.RemaindermanDesignationID
					,Deleted.RemaindermanID
					,Deleted.AccountID
					,GETDATE() 
		INTO TBL_Lookup_RemaindermanDesignation_DeletedRecord(ContactID ,CustomerAccountNumber ,ContactRoleCode ,RemaindermanDesignationID ,RemaindermanID ,AccountID, DeletedDate)
		FROM		dbo.TBL_Lookup_RemaindermanDesignation	LP
		LEFT JOIN	dbo.SYN_IT_ContactAccountRoles	T 
					ON 	LP.ContactID = T.ContactID
					AND	LP.CustomerAccountNumber = T.CustomerAccountNumber
					AND	LP.ContactRoleCode	= T.ContactRoleCode					
		WHERE		T.ContactID IS NULL
		
		--LOGGING
		SET @RowCount=@@RowCount
		SET @LogMessage = 'Incremental data loaded - Physical Delete - ' + @TableName 
		EXEC USP_Lookup_RefreshLog @TableName, 'Completed', 0, @RowCount, @LogMessage, @RunID 
		
		RETURN 0
		
	END TRY
	BEGIN CATCH
		DECLARE @ErrorMessage	NVARCHAR(max)
		DECLARE @ErrorSeverity	INT
		DECLARE @ErrorState		INT

		SET  @ErrorMessage	= 'Incremental data load - [ ' + @TableName + ' ] - Error: ' + Error_Message()
		SET	 @ErrorSeverity	= ERROR_SEVERITY()
		SET	 @ErrorState	= ERROR_STATE()
	    
		EXEC USP_Lookup_RefreshLog @TableName,'Error',0,0, 'Error'   ,@RunID       
	    
		RAISERROR (@ErrorMessage,	-- Message text
				   @ErrorSeverity,	-- Severity
				   @ErrorState		-- State
				   );    
	END CATCH
END