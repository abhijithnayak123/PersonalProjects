/****** Object:  StoredProcedure [dbo].[USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail]    Script Date: 10/16/2013 11:22:21 ******/
IF EXISTS (
		SELECT *
		FROM sys.objects
		WHERE object_id = OBJECT_ID(N'[dbo].[USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail]')
			AND type IN (
				N'P'
				,N'PC'
				)
		)
	DROP PROCEDURE [dbo].[USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail]
GO

/****** Object:  StoredProcedure [dbo].[USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail]    Script Date: 10/16/2013 11:22:21 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/******************************************************************************                      
** Name		 : USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail                      
** Short Desc: To get Gift Addition Detail
**                      
** Full Description:  To get Gift Addition Detail for Beneficiary Payment          
**        
**                              
** Input Arguments: 
	@UserID INT,
	@Type Bit =1--auto    

**         
** Sample Call     

EXEC USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail    100110,0   

                 	
**                      
** Standard declarations                      
**       SET LOCK_TIMEOUT         30000   -- 30 seconds                      
**                       
** Created By: Debajyoti kalita   
** Company   : Kaspick & Company                      
** Project   : BackOffice Integration                      
** Created DT: 16-Oct-13                 
**                                  
*******************************************************************************                
**       Change History                      
*******************************************************************************                
** Date:      Author:	 Bug #     Description:                           
** --------  --------	 ------    -------------------------------------- 
** 01/28/2014 Salih                Modified the hardcoded database name to use the synonyms
** 05/22/2014 Niveditha			   Modified the Anand's code to fetch row per schedule based on the filter
***
*******************************************************************************                      
** Copyright (C) <CopyrightYear,,Year> Kaspick & Company, All Rights Reserved                      
** COMPANY CONFIDENTIAL -- NOT FOR DISTRIBUTION                      
*******************************************************************************/
CREATE PROCEDURE [dbo].[USP_PP_GetValidationEngineBeneficiaryGiftAdditionDetail]
	-- paremeters here                                
	@UserID INT
	,@Type BIT = 1 --auto               
AS
BEGIN TRY
	--  Initial Set statements  --      
	SET NOCOUNT ON;
	SET LOCK_TIMEOUT 30000;-- 30 seconds      

	---Declare Variable
	DECLARE @RuleID INT

	---Assigning value to Variable
	SELECT @RuleID = RuleID
	FROM TBL_PP_ValidationRule
	WHERE RuleName = 'Possibly stale payment calculation'
		AND RuleCategory = 'Beneficiary payment'

	IF OBJECT_ID('tempdb..#TBL_GiftAdditionDateDiff') IS NOT NULL
		DROP TABLE #TBL_GiftAdditionDateDiff

	--- For security Transaction
	SELECT PaymentType
		,StgData.ScheduleID
		,max(DateOfGift) AS DateOfGift
	INTO #TBL_GiftAdditionDateDiff
	FROM TBL_PP_STG_InnoTrustValidationEngineInput StgData
	INNER JOIN TBL_PP_BeneficiaryPayoutSchedule BenPayout
		ON BenPayout.BpScheduleID = StgData.ScheduleID
	INNER JOIN SYN_IT_SecurityTransaction secTran
		ON secTran.CustomerAccountNumber = StgData.CustomerAccountNumber
	INNER JOIN SYN_IT_SecurityTransactionExt secTranExt
		ON secTran.TransactionNumber = secTranExt.TransactionNumber
	INNER JOIN SYN_IT_MasterSecIndex mstIdx
		ON secTran.ISIN = mstIdx.ISIN
	INNER JOIN SYN_IT_SecurityCharacteristicsCodes secCodes
		ON mstIdx.SecurityTypeCode = secCodes.SecurityTypeCode
	WHERE DATEDIFF(DAY, DateOfGift, BenPayout.CreatedDate) < 0
		AND StgData.PaymentType = 'Beneficiarypayment'
		AND AccountPaysActual = 'FALSE'
		AND UserID = @UserID
	GROUP BY PaymentType
		,StgData.ScheduleID

	---For Cash Transaction
	INSERT INTO #TBL_GiftAdditionDateDiff
	SELECT PaymentType
		,StgData.ScheduleID
		,max(CashTranExt.DateOfGift)
	FROM TBL_PP_STG_InnoTrustValidationEngineInput StgData
	INNER JOIN TBL_PP_BeneficiaryPayoutSchedule BenPayout
		ON BenPayout.BpScheduleID = StgData.ScheduleID
	INNER JOIN SYN_IT_CashTransaction CashTran
		ON CashTran.CustomerAccountNumber = StgData.CustomerAccountNumber
	INNER JOIN SYN_IT_CashTransactionExt CashTranExt
		ON CashTran.TransactionNumber = CashTranExt.TransactionNumber
	WHERE DATEDIFF(DAY, DateOfGift, BenPayout.CreatedDate) < 0
		AND StgData.PaymentType = 'Beneficiarypayment'
		AND AccountPaysActual = 'FALSE'
		AND UserID = @UserID
	GROUP BY PaymentType
		,StgData.ScheduleID

	---Return data
	--- For security Transaction
	SELECT PaymentType
		,ScheduleID
		,@RuleID AS RuleID
		,'GiftAdditionDateDiff' AS Attribute
		,(
			CASE 
				WHEN ISNULL(DateOfGift, '') = ''
					THEN 'False'
				ELSE 'True'
				END
			) AS ActualValue
	FROM #TBL_GiftAdditionDateDiff
END TRY

BEGIN CATCH
	SELECT ERROR_MESSAGE() AS ErrorMessage
END CATCH
SET NOCOUNT OFF;